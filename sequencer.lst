                      ; Basic sequencer program
                      ; Can be jumped into at entry address
                      ; Expects stack to be set up
                      
                      ; =========================
                      ; Monitor Function Addresses
                      ; =========================
F804                  inch    equ $F804
F806                  inche   equ $F806
F808                  inchek  equ $F808
F80A                  outch   equ $F80A
F80C                  pdata   equ $F80C
F80E                  pcrlf   equ $F80E
                      
                      ; =========================
                      ; Sequencer Addresses
                      ; =========================
E020                  seq_io_base equ $E020
                      
4000                  seq_data_base equ $4000         ; The base of channel data (4 0x10-length byte arrays for each channel)
4040                  seq_tempo equ $4040             ; The tempo value
4042                  seq_record equ $4042            ; The address in the data of the track being recorded
4044                  seq_temp_tempo equ $4044        ; The temporary tempo storing the values as a tempo is being entered
4046                  seq_record_ch equ $4046         ; The channel to record sequences to
                                                      ; Note: this variable can take on special values to control the flow of the main playback logic
                                                      ; 0: Not recording, and doing standard playback
                                                      ; 0xFF: Playback stopped, should return to main menu
                                                      ; 0xFE: Playback paused, pending channel input from user to specify which channel to record to
4047                  seq_cur_beat equ $4047          ; The current beat being played
4048                  seq_num_spaces equ $4048        ; The number of spaces to print between markings, allows printing extra info in ISR without breaking spacing
4049                  seq_in_delay equ $4049          ; If the sequence is currently in the delay loop, if not the isr should not do anything (can sometimes miss keypresses, but the alternative is a race condition)
4050                  seq_timer_fired equ $4050
                      
                      
                      ; =========================
                      ; Serial Addresses/Config
                      ; =========================
E004                  serial_io_address equ $E004
0011                  serial_base_config equ $11
DFC6                  firq_jump_address equ $dfc6
E008                  timer_io_address equ $E008
                      
                      ; =========================
                      ; Entry
                      ; =========================
E800                          org $E800
                      
                              ;fcb 0x10
                              ;fcb 0x4a
                      
                              ; Print banner
E800  AD9FF80E                jsr [pcrlf]
                      
E804  861E                    lda #30
E806  BDEC01                  jsr printspaces
E809  8EED0F                  ldx #banner
E80C  AD9FF80C                jsr [pdata]
                      
                              ; Print Controls
E810  860D                    lda #13
E812  BDEC01                  jsr printspaces
E815  8EED27                  ldx #controls
E818  AD9FF80C                jsr [pdata]
E81C  AD9FF80E                jsr [pcrlf]
E820  8616                    lda #22
E822  BDEC01                  jsr printspaces
E825  8EED5E                  ldx #controls2
E828  AD9FF80C                jsr [pdata]
                      
                              ; Set default tempo
E82C  CC0014                  ldd #20
E82F  FD4040                  std seq_tempo
                      
E832  BDEB02          redraw  jsr drawscreen
E835  AD9FF808        poll    jsr [inchek]
E839  26FA                    bne poll
                      
E83B  AD9FF804                jsr [inch]
E83F  8166                    cmpa #'f'
E841  2738                    beq crashme
E843  8170                    cmpa #'p'
E845  2716                    beq do_playback
E847  8172                    cmpa #'r'
E849  2717                    beq do_record
E84B  8174                    cmpa #'t'
E84D  2718                    beq do_tempo
E84F  8163                    cmpa #'c'
E851  2719                    beq do_clear
E853  8164                    cmpa #'d'
E855  271A                    beq do_realtime_playback
E857  816D                    cmpa #'m'
E859  271B                    beq do_manual_entry
                      
E85B  20D8                    bra poll
E85D                  do_playback
E85D  BDEA49                  jsr playback
E860  20D0                    bra redraw
E862                  do_record
E862  BDEA08                  jsr record
E865  20CB                    bra redraw
E867                  do_tempo
E867  BDE986                  jsr tempo
E86A  20C6                    bra redraw
E86C                  do_clear
E86C  BDE9D6                  jsr clear
E86F  20C1                    bra redraw
E871                  do_realtime_playback
E871  BDE929                  jsr realtime_playback
E874  20BC                    bra redraw
E876                  do_manual_entry
E876  BDE88D                  jsr manual_entry
E879  20B7                    bra redraw
E87B                  crashme
E87B  8EE884                  ldx #cm_msg
E87E  AD9FF80C                jsr [pdata]
E882                  crashmenow
E882  10                      fcb $10
E883  45                      fcb $45
E884  4D616E4372617368 cm_msg  fcc "ManCrash"
E88C  04                      fcb $4
                      
                      ; =========================
                      ; Manual Entry subroutine
                      ; =========================
E88D                  manual_entry
                              ; Prompt for channel
E88D  8EED97                  ldx #channel_prompt_msg
E890  AD9FF80C                jsr [pdata]
                      
                              ; Get channel to record
E894  AD9FF808        me_wfch jsr [inchek]
E898  27FA                    beq me_wfch
E89A  AD9FF804                jsr [inch]
E89E  8173                    cmpa #'s'
E8A0  277D                    beq me_done
E8A2  8131                    cmpa #'1'
E8A4  2DEE                    blt me_wfch
E8A6  8134                    cmpa #'4'
E8A8  2EEA                    bgt me_wfch
                              
                              ; Echo channel select
E8AA  1F89                    tfr a,b
E8AC  AD9FF80A                jsr [outch]
                      
                              ; Store channel
E8B0  C030                    subb #48
E8B2  F74046                  stb seq_record_ch
                      
                              ; Make channel range 0-3 to calculate record address (stored in x)
E8B5  5A                      decb
E8B6  8610                    lda #0x10
E8B8  3D                      mul
E8B9  8E4000                  ldx #seq_data_base
E8BC  3A                      abx
                      
E8BD  3410                    pshs x
                              ; Print manual message
E8BF  8EED91                  ldx #manual_msg
E8C2  AD9FF80C                jsr [pdata]
E8C6  8605                    lda #5
E8C8  BDEC01                  jsr printspaces
E8CB  3510                    puls x
                      
E8CD  867C                    lda #'|'
E8CF  AD9FF80A                jsr [outch]
                      
E8D3  4F                      clra
E8D4  3402            me_loop pshs a
E8D6  AD9FF808                jsr [inchek]
E8DA  26F8                    bne me_loop
E8DC  AD9FF804                jsr [inch]
                      
                              ; Check for stop
E8E0  8173                    cmpa #'s'
E8E2  273B                    beq me_done
                      
                              ; Do input processing
E8E4  F64046                  ldb seq_record_ch
E8E7  BDEC54                  jsr get_beat
E8EA  26E8                    bne me_loop
                      
                              ; Play Beat
E8EC  3502                    puls a
E8EE  3402                    pshs a
E8F0  BDEC35                  jsr play_beat
                      
                              ; ===Print tick===
E8F3  8602                    lda #2
E8F5  BDEC01                  jsr printspaces
                              
E8F8  E680                    ldb ,x+
E8FA  8601                    lda #1
E8FC                  me_check_bit
E8FC  C501                    bitb #1
E8FE  2608                    bne me_print_val
E900  54                      lsrb
E901  4C                      inca
E902  8109                    cmpa #9
E904  26F6                    bne me_check_bit
E906  86FE                    lda #-2  ; When 48 is added it will turn into a dot
E908                  me_print_val
E908  8B30                    adda #48
E90A  AD9FF80A                jsr [outch]
                      
                              ; Increment beat counter
E90E  3502                    puls a
E910  4C                      inca
E911  8110                    cmpa #16
E913  2DBF                    blt me_loop
                      
                              ; If at end of line, print line terminator and quit
E915  867C                    lda #'|'
E917  AD9FF80A                jsr [outch]
E91B  AD9FF80E                jsr [pcrlf]
                      
E91F                  me_done ; Stop any notes being played
E91F  8EE020                  ldx #seq_io_base
E922  4F                      clra
E923  5F                      clrb
E924  ED84                    std ,x
E926  ED02                    std 2,x
                      
E928  39                      rts
                      
                      
                      ; =========================
                      ; Realtime Playback subroutine
                      ; =========================
E929                  realtime_playback
                              ; Clear any pending played notes (when called internally)
E929  8EE020                  ldx #seq_io_base
E92C  4F                      clra
E92D  5F                      clrb
E92E  ED84                    std ,x
E930  ED02                    std 2,x
                      
E932  8EEDEF                  ldx #realtime_ch_prompt_msg
E935  AD9FF80C                jsr [pdata]
                      
E939  AD9FF808        rp_wfch jsr [inchek]
E93D  27FA                    beq rp_wfch
E93F  AD9FF804                jsr [inch]
E943  8131                    cmpa #'1'
E945  2D35                    blt rp_done
E947  8134                    cmpa #'4'
E949  2E31                    bgt rp_done
                      
                              ; Echo selected channel to console, and give prompt to play data
E94B  3402                    pshs a
E94D  AD9FF80A                jsr [outch]
E951  8EEE18                  ldx #realtime_entry_msg
E954  AD9FF80C                jsr [pdata]
E958  3504                    puls b
                      
                              ; Load the address to write channel data to
E95A  C030                    subb #48
E95C  F74046                  stb seq_record_ch
E95F  5A                      decb
E960  8EE020                  ldx #seq_io_base
E963  3A                      abx
                      
                              ; Get input from user
E964  AD9FF808        rp_play jsr [inchek]
E968  27FA                    beq rp_play
E96A  AD9FF804                jsr [inch]
E96E  8163                    cmpa #'c'
E970  27B7                    beq realtime_playback
                      
                              ; Do processing on input data
E972  F64046                  ldb seq_record_ch
E975  BDEC54                  jsr get_beat
E978  2602                    bne rp_done
                              
E97A  20E8                    bra rp_play
                      
E97C                  rp_done ; Stop any pending playback
E97C  8EE020                  ldx #seq_io_base
E97F  4F                      clra
E980  5F                      clrb
E981  ED84                    std ,x
E983  ED02                    std 2,x
                      
E985  39                      rts
                      
                      ; =========================
                      ; Tempo subroutine
                      ; =========================
E986                  tempo
E986  8EED9C                  ldx #tempo_prompt_msg
E989  AD9FF80C                jsr [pdata]
                      
                              ; Clear temporary tempo
E98D  CC0000                  ldd #0
E990  FD4044                  std seq_temp_tempo
                      
E993  AD9FF808        t_waitc jsr [inchek]
E997  27FA                    beq t_waitc
E999  AD9FF806                jsr [inche]
E99D  812E                    cmpa #'.'
E99F  2723                    beq t_write
E9A1  8130                    cmpa #'0'
E9A3  2D2C                    blt t_done
E9A5  8139                    cmpa #'9'
E9A7  2E28                    bgt t_done
                      
                              ; Convert char to number
E9A9  8030                    suba #48
E9AB  3402                    pshs a
                      
                              ; Multiply existing temporary tempo by 10 and add entered number
E9AD  108E4044                ldy #seq_temp_tempo
E9B1  E621                    ldb 1,y
E9B3  860A                    lda #10
E9B5  3D                      mul
E9B6  1F01                    tfr d,x
E9B8  3504                    puls b
E9BA  3A                      abx
E9BB  AFA4                    stx ,y
                      
                              ; If the number is too big to fit into b before multiplication, write it (gives range of 2559)
E9BD  8C00FF                  cmpx #255
E9C0  2E02                    bgt t_write
                      
E9C2  20CF                    bra t_waitc
E9C4  FC4044          t_write ldd seq_temp_tempo
E9C7  FD4040                  std seq_tempo
                      
E9CA  8EEE58                  ldx #tempo_set_msg
E9CD  AD9FF80C                jsr [pdata]
E9D1  AD9FF80E        t_done  jsr [pcrlf]
E9D5  39                      rts
                      
                      ; =========================
                      ; Clear subroutine
                      ; =========================
E9D6                  clear
                              ; Prompt for channel
E9D6  8EEDC5                  ldx #clear_prompt_msg
E9D9  AD9FF80C                jsr [pdata]
                      
                              ; Get channel to clear
E9DD  AD9FF808        cl_wfch jsr [inchek]
E9E1  27FA                    beq cl_wfch
E9E3  AD9FF804                jsr [inch]
E9E7  8131                    cmpa #'1'
E9E9  2D1B                    blt cl_done
E9EB  8134                    cmpa #'4'
E9ED  2E17                    bgt cl_done
                      
                              ; Echo channel select
E9EF  1F89                    tfr a,b
E9F1  AD9FF80A                jsr [outch]
                      
                              ; Calculate address of sequence data
E9F5  C031                    subb #49
E9F7  8610                    lda #0x10
E9F9  3D                      mul
E9FA  8E4000                  ldx #seq_data_base
E9FD  3A                      abx
                              
                              ; Clear the channel of existing data
E9FE  C610                    ldb #0x10
EA00  4F                      clra
EA01  A780            cl_loop sta ,x+
EA03  5A                      decb
EA04  26FB                    bne cl_loop
EA06  39              cl_done rts
                      
                      ; =========================
                      ; Record subroutine
                      ; =========================
EA07  39              rc_quit rts
EA08                  record
                              ; Prompt for channel
EA08  8EED97                  ldx #channel_prompt_msg
EA0B  AD9FF80C                jsr [pdata]
                      
                              ; Get channel to record
EA0F  AD9FF808        rc_wfch jsr [inchek]
EA13  27FA                    beq rc_wfch
EA15  AD9FF804                jsr [inch]
EA19  8173                    cmpa #'s'
EA1B  27EA                    beq rc_quit
EA1D  8131                    cmpa #'1'
EA1F  2DEE                    blt rc_wfch
EA21  8134                    cmpa #'4'
EA23  2EEA                    bgt rc_wfch
                              
                              ; Echo channel select
EA25  1F89                    tfr a,b
EA27  AD9FF80A                jsr [outch]
                              
                              ; Store selected channel
EA2B  C030                    subb #48  ; Convert '1'-'4' to 1-4
EA2D  F74046                  stb seq_record_ch
                      
                              ; Make channel range 0-3 to calculate record address
EA30  5A                      decb
EA31  8610                    lda #0x10
EA33  3D                      mul
EA34  8E4000                  ldx #seq_data_base
EA37  3A                      abx
EA38  BF4042                  stx seq_record
                      
                              ; Print record message
EA3B  8EED8C                  ldx #record_msg
EA3E  AD9FF80C                jsr [pdata]
EA42  8606                    lda #6
EA44  BDEC01                  jsr printspaces
                      
EA47  200F                    bra pb_common
                      
                      
                      ; =========================
                      ; Playback subroutine
                      ; =========================
EA49                  playback
                              ; Print playback symbol
EA49  8EED83                  ldx #playback_msg
EA4C  AD9FF80C                jsr [pdata]
EA50  8607                    lda #7
EA52  BDEC01                  jsr printspaces
                      
EA55  7F4046                  clr seq_record_ch       ; Set playback channel to 0 (No recording)
EA58                  pb_common       
                              ; Enable interrupts for serial input, allowing processing during playback
EA58  7F4049                  clr seq_in_delay        ; Make sure the interrupts don't mess with anything before it is in the delay
EA5B  BEDFC6                  ldx firq_jump_address
EA5E  3411                    pshs cc,x               ; Save the cc register and the previous irq data, so if firqs are already enabled then they will remain enabled after the function exits
EA60  8EEC7C                  ldx #playback_isr
EA63  BFDFC6                  stx firq_jump_address   ; Set the new irq function
EA66  8680                    lda #0x80               ; Enable serial irqs
EA68  BDEC76                  jsr serial_set_interrupt
EA6B  8601                    lda #1                  ; Enable timer irqs
EA6D  B7E008                  sta timer_io_address
EA70  1CBF                    andcc #0xBF             ; Enable FIRQ
EA72  7F4050                  clr seq_timer_fired             ; Clear pending timer interrupts
                      
                              ; Set the default number of spaces to print between marks
EA75  8602                    lda #2
EA77  B74048                  sta seq_num_spaces
                      
EA7A  867C            pb_run  lda #'|'
EA7C  AD9FF80A                jsr [outch]
                              
EA80  7F4047                  clr seq_cur_beat        ; Set the currently played beat to 0
                              
EA83                  pb_loop 
                              ; Delay
EA83  7C4049                  inc seq_in_delay        ; Note this is under the assumption that pb_loop is always jumped to with seq_in_delay cleared
EA86  BDEC18                  jsr delay_tempo
                      
                              ; Check if input is pending for changing record channel
EA89  B64046          pb_wait lda seq_record_ch
EA8C  81FE                    cmpa #0xFE
EA8E  27F9                    beq pb_wait
                              
EA90  7F4049                  clr seq_in_delay
                      
                              ; Check if during delay an interrupt fired which stopped playback
EA93  B64046                  lda seq_record_ch
EA96  81FF                    cmpa #0xFF
EA98  2752                    beq pb_done
                      
                              ; Play Beat
EA9A  B64047                  lda seq_cur_beat
EA9D  BDEC35                  jsr play_beat
                      
                              ; ===Print tick===
                      
                              ; Print number of spaces required, then reset the value
EAA0  B64048                  lda seq_num_spaces
EAA3  BDEC01                  jsr printspaces
EAA6  8602                    lda #2
EAA8  B74048                  sta seq_num_spaces
                              
                              ; Tick will be different for playback vs recording
EAAB  B64046                  lda seq_record_ch
EAAE  2604                    bne pb_rec
                              
                              ; Character to display during playback
EAB0  862E                    lda #'.'
EAB2  2018                    bra pb_drawch
                              
EAB4                  pb_rec  ; This code will run when it is recording
EAB4  BE4042                  ldx seq_record
EAB7  B64047                  lda seq_cur_beat
EABA  E686                    ldb a,x
                              
EABC  8601                    lda #1
EABE                  pb_check_bit
EABE  C501                    bitb #1
EAC0  2608                    bne pb_print_val
EAC2  54                      lsrb
EAC3  4C                      inca
EAC4  8109                    cmpa #9
EAC6  26F6                    bne pb_check_bit
EAC8  86FE                    lda #-2  ; When 48 is added it will turn into a dot
EACA                  pb_print_val
EACA  8B30                    adda #48
EACC                  pb_drawch
EACC  AD9FF80A                jsr [outch]
                      
                              ; Increment beat counter
EAD0  B64047                  lda seq_cur_beat
EAD3  4C                      inca
EAD4  B74047                  sta seq_cur_beat
EAD7  8110                    cmpa #16
EAD9  2DA8                    blt pb_loop
                      
                              ; If at end of line, start a new one
EADB  867C                    lda #'|'
EADD  AD9FF80A                jsr [outch]
EAE1  AD9FF80E                jsr [pcrlf]
EAE5  860F                    lda #15
EAE7  BDEC01                  jsr printspaces
EAEA  208E                    bra pb_run
                      
EAEC                  pb_done ; Stop serial interrupts
EAEC  3511                    puls cc,x
EAEE  BFDFC6                  stx firq_jump_address
EAF1  4F                      clra
EAF2  BDEC76                  jsr serial_set_interrupt
EAF5  7FE008                  clr timer_io_address
                      
                              ; Stop any notes being played
EAF8  8EE020                  ldx #seq_io_base
EAFB  4F                      clra
EAFC  5F                      clrb
EAFD  ED84                    std ,x
EAFF  ED02                    std 2,x
                      
EB01  39                      rts
                      
                      
                      ; =========================
                      ; Draw Screen subroutine
                      ; =========================
                      ; Draws the screen displaying the sequence
EB02                  drawscreen
EB02  AD9FF80E                jsr [pcrlf]
EB06  5F                      clrb
EB07  3404                    pshs b
                              
EB09                  ds_drawrow
                              ; Print out the pre-padding
EB09  860A                    lda #10 ; Pre-padding without channel id
                      
                              ; Channel headers get lower spacing for channel id prefix
EB0B  C103                    cmpb #3
EB0D  2D06                    blt ds_pleadspaces
EB0F  C107                    cmpb #7
EB11  2C02                    bge ds_pleadspaces
EB13  8607                    lda #7 ; Pre-padding with channel id
EB15                  ds_pleadspaces
EB15  BDEC01                  jsr printspaces
                      
EB18  C103                    cmpb #3
EB1A  2D18                    blt ds_printheader
EB1C  C107                    cmpb #7
EB1E  2C14                    bge ds_printheader
EB20  1F98                    tfr b,a
EB22  8B2E                    adda #46
EB24  AD9FF80A                jsr [outch]
EB28  863A                    lda #':'
EB2A  AD9FF80A                jsr [outch]
EB2E  8620                    lda #32
EB30  AD9FF80A                jsr [outch]
                      
EB34                  ds_printheader
                              ; Print row title
EB34  58                      lslb
EB35  8EEE65                  ldx #header_table
EB38  3A                      abx
EB39  AE84                    ldx ,x
EB3B  AD9FF80C                jsr [pdata]
                      
                              ; Print space
EB3F  8620                    lda #32
EB41  AD9FF80A                jsr [outch]
                      
                              ; Print table right border
EB45  C600                    ldb #0
EB47                  ds_draw_border
EB47  3502                    puls a
EB49  3402                    pshs a
EB4B  8100                    cmpa #0
EB4D  2702                    beq ds_fr_c
EB4F  861D                    lda #0x1D  ; If A is not the first row, set to '|' (0x7C), else set to 0x5C
EB51  8B5F            ds_fr_c adda #0x5F
EB53  AD9FF80A                jsr [outch]
EB57  C101                    cmpb #1
EB59  10270093                lbeq ds_loop
                      
                              ; Print contents of table
EB5D  3502                    puls a
EB5F  3402                    pshs a
EB61  8100                    cmpa #0
EB63  2748                    beq ds_top_row
EB65  8101                    cmpa #1
EB67  2759                    beq ds_beats_row
EB69  8102                    cmpa #2
EB6B  273C                    beq ds_mid_row
EB6D  8107                    cmpa #7
EB6F  2738                    beq ds_mid_row
                      
                              ; Else channel row
                              ; Load base address of sequence
EB71  3502                    puls a
EB73  3402                    pshs a
EB75  8003                    suba #3
EB77  C610                    ldb #0x10
EB79  3D                      mul
EB7A  8E4000                  ldx #seq_data_base
EB7D  3A                      abx
                              
EB7E  4F                      clra
EB7F  3402                    pshs a
EB81                  ds_draw_channel_beat
EB81  8602                    lda #2
EB83  BDEC01                  jsr printspaces
EB86  E680                    ldb ,x+
EB88  8601                    lda #1
                      
EB8A                  ds_channel_check_bit
EB8A  C501                    bitb #1
EB8C  2608                    bne ds_print_channel_val
EB8E  54                      lsrb
EB8F  4C                      inca
EB90  8109                    cmpa #9
EB92  26F6                    bne ds_channel_check_bit
EB94  86F0                    lda #-16  ; When 48 is added it will turn into a space
                      
EB96                  ds_print_channel_val
EB96  8B30                    adda #48
EB98  AD9FF80A                jsr [outch]
                      
EB9C  3502                    puls a
EB9E  4C                      inca
EB9F  3402                    pshs a
EBA1  8110                    cmpa #16
                      
EBA3  26DC                    bne ds_draw_channel_beat
EBA5  3502                    puls a
                      
EBA7  2042                    bra ds_border_close
                      
                      
EBA9                  ds_mid_row
EBA9  862D                    lda #'-'
EBAB  2002                    bra ds_draw_row
EBAD                  ds_top_row
EBAD  865F                    lda #'_'
EBAF                  ds_draw_row
EBAF  3402                    pshs a
EBB1  C630                    ldb #0x30       ; Row Width: 48
EBB3  3502            ds_dr_l puls a          ; Print variable on stack b times
EBB5  3402                    pshs a
EBB7  AD9FF80A                jsr [outch]
EBBB  5A                      decb
EBBC  26F5                    bne ds_dr_l
EBBE  3502                    puls a
EBC0  2029                    bra ds_border_close
                      
EBC2                  ds_beats_row
EBC2  C601                    ldb #1
                      
EBC4                  ds_print_beat
                              ; Print space
EBC4  8620                    lda #32
EBC6  AD9FF80A                jsr [outch]
                      
                              ; Print tens digit, or space
EBCA  8620                    lda #32
EBCC  C10A                    cmpb #10
EBCE  2D02                    blt ds_beat_print_tens
EBD0  8631                    lda #'1'
EBD2                  ds_beat_print_tens
EBD2  AD9FF80A                jsr [outch]
                      
                              ; Print ones digit
EBD6  1F98                    tfr b,a
EBD8  C10A                    cmpb #10
EBDA  2D02                    blt ds_beat_skip_subtract
EBDC  800A                    suba #10
EBDE                  ds_beat_skip_subtract
EBDE  8B30                    adda #48
EBE0  AD9FF80A                jsr [outch]
                      
EBE4  5C                      incb
EBE5  C110                    cmpb #16
EBE7  2FDB                    ble ds_print_beat
                      
EBE9  2000                    bra ds_border_close
                      
                      
                              ; Print table border
EBEB                  ds_border_close
EBEB  C601                    ldb #1
EBED  16FF57                  lbra ds_draw_border
EBF0                  ds_loop
EBF0  AD9FF80E                jsr [pcrlf]
EBF4  3504                    puls b
EBF6  5C                      incb
                      
EBF7  C108                    cmpb #8
EBF9  2705                    beq ds_done
                              
EBFB  3404                    pshs b
EBFD  16FF09                  lbra ds_drawrow
                      
EC00  39              ds_done rts
                      
                      ; =========================
                      ; Print Spaces Subroutine
                      ; =========================
                      ; Prints number of spaces in A register
EC01                  printspaces
EC01  3404                    pshs b
EC03  1F89                    tfr a,b
EC05  5D              ps_loop tstb
EC06  270D                    beq ps_done
EC08  3404                    pshs b
EC0A  8620                    lda #32  ; Load with space
EC0C  AD9FF80A                jsr [outch]
EC10  3504                    puls b
EC12  5A                      decb
EC13  20F0                    bra ps_loop
EC15  3504            ps_done puls b
EC17  39                      rts
                      
                      ; =========================
                      ; Delay Subroutine
                      ; =========================
EC18                  delay_tempo
EC18  3410                    pshs x
EC1A  8E0000                  ldx #0
EC1D  C601                    ldb #1
EC1F  7D4050                  tst seq_timer_fired
EC22  2605                    bne d_nowait
EC24  7D4050          d_wait  tst seq_timer_fired
EC27  27FB                    beq d_wait
EC29                  d_nowait
EC29  7F4050                  clr seq_timer_fired
EC2C  3A                      abx
EC2D  BC4040                  cmpx seq_tempo
EC30  26F2                    bne d_wait
                      
EC32  3510                    puls x
EC34  39                      rts
                      
                      ; =========================
                      ; Play Beat Subroutine
                      ; =========================
                      ; The a register should contain the offset
                      ; The b,x register is saved
EC35                  play_beat
EC35  3414                    pshs x,b
                      
                              ; Setup initial address
EC37  8E4000                  ldx #seq_data_base
EC3A  1F89                    tfr a,b
EC3C  3A                      abx
EC3D  108EE020                ldy #seq_io_base
                      
                              ; Set channel count to transfer
EC41  C604                    ldb #4
                              
                              ; Write each channel's value for the given beat
EC43  3404            p_b_wch pshs b
EC45  A684                    lda ,x
EC47  A7A0                    sta ,y+
EC49  C610                    ldb #16
EC4B  3A                      abx
EC4C  3504                    puls b
EC4E  5A                      decb
EC4F  26F2                    bne p_b_wch
                      
EC51  3514                    puls x,b
EC53  39                      rts
                      
                      ; =========================
                      ; Get Beat Subroutine
                      ; =========================
                      ; a is the beat to get
                      ; b is the channel to record to (used in decoding logic)
                      ; x is the address to store data in
                      ; Returns: cc.z = 0 on failure decoding, cc.z = 1 on successful decoding
EC54                  get_beat
EC54  812E                    cmpa #'.'
EC56  2716                    beq gb_clrv
EC58  8120                    cmpa #32
EC5A  2717                    beq gb_done
EC5C  8131                    cmpa #'1'
EC5E  2D13                    blt gb_done
EC60  8138                    cmpa #'8'
EC62  2E0F                    bgt gb_done
                      
                              ; a is now loaded with a character between 1 and 8
                              ; Convert entered character to value
EC64  8030                    suba #48
EC66  C601                    ldb #1
EC68  4A              gb_conv deca
EC69  2704                    beq gb_write
EC6B  58                      lslb
EC6C  20FA                    bra gb_conv
EC6E                  gb_clrv ; Create a value to clear when writing to memory
EC6E  5F                      clrb
EC6F                  gb_write
                              ; Write the calculated value to the sequencer memory
EC6F  E784                    stb ,x
                      
                              ; Dirty hack to make it return success on either space or successful decoding
EC71  8620                    lda #32
EC73  8120            gb_done cmpa #32
EC75  39                      rts
                      
                      ; =========================
                      ; Serial Interrupt Routines
                      ; =========================
                      ; a register: 0x80 for enable interrupts, 0 for disable
EC76                  serial_set_interrupt
EC76  8B11                    adda #serial_base_config
EC78  B7E004                  sta serial_io_address
EC7B  39                      rts
                      
EC7C                  playback_isr
EC7C  3406                    pshs a,b
                      
EC7E  AD9FF808                jsr [inchek]            ; Check if interrupt is from key press
EC82  2613                    bne isr_key
                      
EC84  7DE009                  tst timer_io_address+1  ; Check if interrupt is from timer
EC87  2603                    bne isr_timer
                      
EC89  160080                  lbra isr_end            ; Else ignore
                      
EC8C                  isr_timer
EC8C  7FE009                  clr timer_io_address+1
EC8F  8601                    lda #1
EC91  B74050                  sta seq_timer_fired
EC94  160075                  lbra isr_end
                      
EC97  AD9FF804        isr_key jsr [inch]
                      
                              ; Check if it is okay to do handling of incoming serial data
EC9B  F64049                  ldb seq_in_delay
EC9E  276C                    beq isr_end
                      
                              ; Process universal keypresses
ECA0  8173                    cmpa #'s'
ECA2  2745                    beq isr_stop_playback
ECA4  8170                    cmpa #'p'
ECA6  2758                    beq isr_do_playback
ECA8  8172                    cmpa #'r'
ECAA  2744                    beq isr_do_record
                      
                              ; Process keypresses depending on recording/playback/expecting channel data
ECAC  F64046                  ldb seq_record_ch
ECAF  275B                    beq isr_end     ; Nothing special to do during playback, only universal keypresses
ECB1  C1FF                    cmpb #0xFF
ECB3  2757                    beq isr_end     ; Don't do anything special if it is stopping
ECB5  C1FE                    cmpb #0xFE
ECB7  2712                    beq isr_sel_ch  ; Do channel selection if it is waiting for a channel
                              
                              ; Everything else is recording and b now contains the channel to record to
ECB9  3414                    pshs x,b
ECBB  F64047                  ldb seq_cur_beat
ECBE  BE4042                  ldx seq_record
ECC1  3A                      abx
ECC2  3504                    puls b
ECC4  BDEC54                  jsr get_beat
ECC7  3510                    puls x
                              
ECC9  2041                    bra isr_end
                      
ECCB                  isr_sel_ch
ECCB  8131                    cmpa #'1'
ECCD  2D3D                    blt isr_end
ECCF  8134                    cmpa #'4'
ECD1  2E39                    bgt isr_end
                              
                              ; Store selected channel
ECD3  8030                    suba #48  ; Convert '1'-'4' to 1-4
ECD5  B74046                  sta seq_record_ch
                      
                              ; Make channel range 0-3 to calculate record address
ECD8  3410                    pshs x
ECDA  4A                      deca
ECDB  C610                    ldb #0x10
ECDD  3D                      mul
ECDE  8E4000                  ldx #seq_data_base
ECE1  3A                      abx
ECE2  BF4042                  stx seq_record
ECE5  3510                    puls x
                      
ECE7  2023                    bra isr_end
                      
ECE9                  isr_stop_playback
ECE9  86FF                    lda #0xFF
ECEB  B74046                  sta seq_record_ch
ECEE  201C                    bra isr_end
                      
ECF0                  isr_do_record
                              ; Print command to display
ECF0  8652                    lda #'R'
ECF2  AD9FF80A                jsr [outch]
ECF6  7A4048                  dec seq_num_spaces
                      
                              ; Set to wait for pending press to switch recording channel
ECF9  86FE                    lda #0xFE
ECFB  B74046                  sta seq_record_ch
ECFE  200C                    bra isr_end
                      
ED00                  isr_do_playback
                              ; Print command to display
ED00  8650                    lda #'P'
ED02  AD9FF80A                jsr [outch]
ED06  7A4048                  dec seq_num_spaces
                      
                              ; Set to playback
ED09  7F4046                  clr seq_record_ch
                      
ED0C  3506            isr_end puls a,b
ED0E  3B                      rti
                      
                      ; =========================
                      ; Data Section
                      ; =========================
ED0F  2A2A2A53575450432053657175656E6365722A2A2A0D0A banner  fcc "***SWTPC Sequencer***\r\n"
ED26  04                      fcb $04
ED27                  controls
ED27  503A20506C6179202D20523A205265636F7264202D20533A2053746F70202D20433A20436C656172204368202D20543A2054656D706F         fcc "P: Play - R: Record - S: Stop - C: Clear Ch - T: Tempo"
ED5D  04                      fcb $04
ED5E                  controls2
ED5E  443A2044697265637420506C61796261636B202D204D3A204D616E75616C20456E747279         fcc "D: Direct Playback - M: Manual Entry"
ED82  04                      fcb $04
ED83                  playback_msg
ED83  506C61796261636B         fcc "Playback"
ED8B  04                      fcb $04
ED8C                  record_msg
ED8C  3A526563                fcc ":Rec"
ED90  04                      fcb $04
ED91                  manual_msg
ED91  3A4D616E75              fcc ":Manu"
ED96  04                      fcb $04
ED97                  channel_prompt_msg
ED97  43683F20                fcc "Ch? "
ED9B  04                      fcb $04
ED9C                  tempo_prompt_msg
ED9C  456E7465722054656D706F20282E20746F20656E642C206F7468657220746F2061626F7274293A20         fcc "Enter Tempo (. to end, other to abort): "
EDC4  04                      fcb $04
EDC5                  clear_prompt_msg
EDC5  456E746572204368616E6E656C20746F20436C65617220286F7468657220746F2061626F7274293A20         fcc "Enter Channel to Clear (other to abort): "
EDEE  04                      fcb $04
EDEF                  realtime_ch_prompt_msg
EDEF  456E746572204368616E6E656C20746F20506C617920286F7468657220746F2061626F7274293A20         fcc "Enter Channel to Play (other to abort): "
EE17  04                      fcb $04
EE18                  realtime_entry_msg
EE18  0A0D5072657373204B65797320746F20506C61792E2E2E20286320746F206368616E6765206368616E6E656C2C206F7468657220746F2061626F7274290A0D         fcc "\n\rPress Keys to Play... (c to change channel, other to abort)\n\r"
EE57  04                      fcb $04
EE58                  tempo_set_msg
EE58  202D2054656D706F20536574         fcc " - Tempo Set"
EE64  04                      fcb $04
EE65                  header_table
EE65  EE75                    fdb header_blank
EE67  EE7A                    fdb header_beat
EE69  EE75                    fdb header_blank
EE6B  EE7F                    fdb header_ch1
EE6D  EE84                    fdb header_ch2
EE6F  EE89                    fdb header_ch3
EE71  EE8E                    fdb header_ch4
EE73  EE75                    fdb header_blank
                      
EE75                  header_blank
EE75  20202020                fcc "    "
EE79  04                      fcb $04
                      
EE7A                  header_beat
EE7A  42656174                fcc "Beat"
EE7E  04                      fcb $04
                      
EE7F                  header_ch1
EE7F  4C656164                fcc "Lead"
EE83  04                      fcb $04
                      
EE84                  header_ch2
EE84  42617373                fcc "Bass"
EE88  04                      fcb $04
                      
EE89                  header_ch3
EE89  43687264                fcc "Chrd"
EE8D  04                      fcb $04
                      
EE8E                  header_ch4
EE8E  4472756D                fcc "Drum"
EE92  04                      fcb $04
